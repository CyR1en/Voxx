plugins {
    id 'java'
    id 'application'
    id 'org.javamodularity.moduleplugin' version '1.8.12'
    id 'org.openjfx.javafxplugin' version '0.0.13'
    id 'org.beryx.jlink' version '2.25.0'
}

repositories {
    mavenCentral()
}

ext {
    junitVersion = '5.9.1'
}

application {
    mainModule = 'com.cyr1en.voxx.client'
    mainClass = 'com.cyr1en.voxx.client.VoxxApplication'
}

javafx {
    version = '17.0.2'
    modules = ['javafx.controls', 'javafx.fxml']
}

dependencies {
    implementation('com.dlsc.formsfx:formsfx-core:11.5.0') {
        exclude(group: 'org.openjfx')
    }
    implementation 'org.jetbrains:annotations:24.0.0'
    implementation group: 'org.json', name: 'json', version: '20230227'
    implementation project(':voxx-commons')

    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitVersion}")
}

test {
    useJUnitPlatform()
}

jlink {
    imageZip = project.file("${buildDir}/distributions/Voxx-${javafx.platform.classifier}.zip")
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = 'Voxx'
    }
    jpackage {
        imageName = 'Voxx'
        vendor = 'CyR1en'

        resourceDir = file("$buildDir/resources")
        icon = "${resourceDir}/main/Voxx.ico"

        installerName = 'Voxx-Installer'
        skipInstaller = project.hasProperty('skipInstaller') ?
                project.getProperties().get('skipInstaller') : false
        installerType = 'msi'
        installerOptions = ['--win-menu', '--win-shortcut']
    }
    forceMerge('log4j-api')
}

jlinkZip {
    group = 'distribution'
}